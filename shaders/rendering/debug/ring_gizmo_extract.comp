#version 430 core

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

// Cell data structure for compute shader
struct ComputeCell {
    // Physics:
    vec4 positionAndMass;   // x, y, z, mass
    vec4 velocity;          // x, y, z, padding
    vec4 acceleration;      // x, y, z, padding
    vec4 prevAcceleration;  // x, y, z, padding
    vec4 orientation;       // Quaternion to (prevent gimbal lock)
    vec4 genomeOrientation; // Quaternion for genome-derived orientation - NEVER affected by physics
    vec4 angularVelocity;   // Pseudo-vector for easy math
    vec4 angularAcceleration;       // Pseudo-vector for easy math
    vec4 prevAngularAcceleration;   // Pseudo-vector for easy math
    // Internal:
    vec4 signallingSubstances; // 4 substances for now
    int modeIndex;  // absolute index of the cell's mode
    float age; // also used for split timer
    float toxins;
    float nitrates;
    int adhesionIndices[20];
    
    // Padding to maintain 16-byte alignment for the entire struct
    uint _padding[4];
};

struct GPUModeAdhesionSettings
{
    int canBreak;                 // bool -> int (4 bytes)
    float breakForce;
    float restLength;
    float linearSpringStiffness;
    float linearSpringDamping;
    float orientationSpringStiffness;
    float orientationSpringDamping;
    float maxAngularDeviation;
    float twistConstraintStiffness;
    float twistConstraintDamping;
    int enableTwistConstraint;    // bool -> int (4 bytes)
    int _padding;                 // pad to 48 bytes
};

// GPU Mode structure
struct GPUMode {
    vec4 color;           // R, G, B, padding
    vec4 orientationA;    // Quaternion
    vec4 orientationB;    // Quaternion
    vec4 splitDirection;  // x, y, z, padding
    ivec2 childModes;     // mode indices for children
    float splitInterval;
    int genomeOffset;
    GPUModeAdhesionSettings adhesionSettings;
    int parentMakeAdhesion;
    int childAKeepAdhesion;
    int childBKeepAdhesion;
    int maxAdhesions;
    float flagellocyteThrustForce;
    float _padding1;
    float _padding2;
    float _padding3;
};

// Ring vertex data - each cell generates 2 rings (blue and red) with thickness
struct RingVertex {
    vec4 position;    // World position (w unused, but needed for alignment)
    vec4 color;       // RGB color (a unused, but needed for alignment)
};

// Input: Cell data
layout(std430, binding = 0) buffer CellBuffer {
    ComputeCell cells[];
};

// Input: Mode data
layout(std430, binding = 1) buffer ModeBuffer {
    GPUMode modes[];
};

// Output: Ring vertices
layout(std430, binding = 2) buffer RingBuffer {
    RingVertex ringVertices[];
};

// Cell count
layout(std430, binding = 3) buffer CellCountBuffer {
    uint totalCellCount;
    uint liveCellCount;
    uint totalAdhesionCount;
    uint freeAdhesionTop;
};

// Convert quaternion to rotation matrix
mat3 quatToMat3(vec4 quat) {
    float x = quat.x, y = quat.y, z = quat.z, w = quat.w;
    float x2 = x + x, y2 = y + y, z2 = z + z;
    float xx = x * x2, xy = x * y2, xz = x * z2;
    float yy = y * y2, yz = y * z2, zz = z * z2;
    float wx = w * x2, wy = w * y2, wz = w * z2;

    return mat3(
        1.0 - (yy + zz), xy + wz, xz - wy,
        xy - wz, 1.0 - (xx + zz), yz + wx,
        xz + wy, yz - wx, 1.0 - (xx + yy)
    );
}

void main() {
    uint cellIndex = gl_GlobalInvocationID.x;
    
    if (cellIndex >= totalCellCount) return;
    
    ComputeCell cell = cells[cellIndex];
    vec3 cellPos = cell.positionAndMass.xyz;
    float cellRadius = pow(cell.positionAndMass.w, 1.0/3.0);
    
    // Get the mode for this cell
    GPUMode mode = modes[cell.modeIndex];
    
    // Convert quaternion to rotation matrix for cell orientation
    mat3 cellRotMatrix = quatToMat3(cell.orientation);
    
    // Get the split direction vector (already normalized)
    vec3 splitDirection = normalize(mode.splitDirection.xyz);
    
    // Transform split direction to world space using cell orientation
    vec3 worldSplitDirection = cellRotMatrix * splitDirection;
    
    // Calculate ring dimensions based on cell size
    float outerRadius = cellRadius * 1.2; // 2x the cell radius
    float innerRadius = cellRadius * 1.4; // 1.5x the cell radius for thickness
    
    // Calculate ring center offset from cell center
    float offsetDistance = 0.001; // Small offset to prevent z-fighting
    
    // Generate two rings: one blue, one red, offset in opposite directions
    vec3 blueRingCenter = cellPos + worldSplitDirection * offsetDistance;
    vec3 redRingCenter = cellPos - worldSplitDirection * offsetDistance;
    
    // Calculate perpendicular vectors to create the ring plane
    vec3 up = vec3(0.0, 1.0, 0.0);
    vec3 right = normalize(cross(worldSplitDirection, up));
    up = normalize(cross(right, worldSplitDirection));
    
    // Number of segments per ring (more segments = smoother ring)
    const int segmentsPerRing = 32;
    
    // Each ring: segmentsPerRing * 6 vertices (2 triangles per segment)
    uint blueBase = cellIndex * segmentsPerRing * 6 * 2;
    uint redBase = blueBase + segmentsPerRing * 6;
    
    // --- Blue Ring ---
    for (int i = 0; i < segmentsPerRing; i++) {
        float angle1 = (2.0 * 3.14159 * float(i)) / float(segmentsPerRing);
        float angle2 = (2.0 * 3.14159 * float((i + 1) % segmentsPerRing)) / float(segmentsPerRing);
        
        vec3 inner1 = blueRingCenter + (right * cos(angle1) + up * sin(angle1)) * innerRadius;
        vec3 outer1 = blueRingCenter + (right * cos(angle1) + up * sin(angle1)) * outerRadius;
        vec3 inner2 = blueRingCenter + (right * cos(angle2) + up * sin(angle2)) * innerRadius;
        vec3 outer2 = blueRingCenter + (right * cos(angle2) + up * sin(angle2)) * outerRadius;
        
        // Triangle 1: inner1, inner2, outer1 (reversed winding)
        ringVertices[blueBase + i * 6 + 0].position = vec4(inner1, 1.0);
        ringVertices[blueBase + i * 6 + 0].color = vec4(1.0, 0.0, 0.0, 1.0);
        ringVertices[blueBase + i * 6 + 1].position = vec4(inner2, 1.0);
        ringVertices[blueBase + i * 6 + 1].color = vec4(1.0, 0.0, 0.0, 1.0);
        ringVertices[blueBase + i * 6 + 2].position = vec4(outer1, 1.0);
        ringVertices[blueBase + i * 6 + 2].color = vec4(1.0, 0.0, 0.0, 1.0);
        
        // Triangle 2: outer1, inner2, outer2 (reversed winding)
        ringVertices[blueBase + i * 6 + 3].position = vec4(outer1, 1.0);
        ringVertices[blueBase + i * 6 + 3].color = vec4(1.0, 0.0, 0.0, 1.0);
        ringVertices[blueBase + i * 6 + 4].position = vec4(inner2, 1.0);
        ringVertices[blueBase + i * 6 + 4].color = vec4(1.0, 0.0, 0.0, 1.0);
        ringVertices[blueBase + i * 6 + 5].position = vec4(outer2, 1.0);
        ringVertices[blueBase + i * 6 + 5].color = vec4(1.0, 0.0, 0.0, 1.0);
    }
    
    // --- Red Ring ---
    for (int i = 0; i < segmentsPerRing; i++) {
        float angle1 = (2.0 * 3.14159 * float(i)) / float(segmentsPerRing);
        float angle2 = (2.0 * 3.14159 * float((i + 1) % segmentsPerRing)) / float(segmentsPerRing);
        
        vec3 inner1 = redRingCenter + (right * cos(angle1) + up * sin(angle1)) * innerRadius;
        vec3 outer1 = redRingCenter + (right * cos(angle1) + up * sin(angle1)) * outerRadius;
        vec3 inner2 = redRingCenter + (right * cos(angle2) + up * sin(angle2)) * innerRadius;
        vec3 outer2 = redRingCenter + (right * cos(angle2) + up * sin(angle2)) * outerRadius;
        
        // Triangle 1: inner1, outer1, inner2
        ringVertices[redBase + i * 6 + 0].position = vec4(inner1, 1.0);
        ringVertices[redBase + i * 6 + 0].color = vec4(0.0, 0.0, 1.0, 1.0);
        ringVertices[redBase + i * 6 + 1].position = vec4(outer1, 1.0);
        ringVertices[redBase + i * 6 + 1].color = vec4(0.0, 0.0, 1.0, 1.0);
        ringVertices[redBase + i * 6 + 2].position = vec4(inner2, 1.0);
        ringVertices[redBase + i * 6 + 2].color = vec4(0.0, 0.0, 1.0, 1.0);
        
        // Triangle 2: outer1, outer2, inner2
        ringVertices[redBase + i * 6 + 3].position = vec4(outer1, 1.0);
        ringVertices[redBase + i * 6 + 3].color = vec4(0.0, 0.0, 1.0, 1.0);
        ringVertices[redBase + i * 6 + 4].position = vec4(outer2, 1.0);
        ringVertices[redBase + i * 6 + 4].color = vec4(0.0, 0.0, 1.0, 1.0);
        ringVertices[redBase + i * 6 + 5].position = vec4(inner2, 1.0);
        ringVertices[redBase + i * 6 + 5].color = vec4(0.0, 0.0, 1.0, 1.0);
    }
} 
