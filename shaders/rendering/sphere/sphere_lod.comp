#version 430 core

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

// Cell data structure for compute shader (must match CellManager)
struct ComputeCell {
    // Physics:
    vec4 positionAndMass;   // x, y, z, mass
    vec4 velocity;          // x, y, z, padding
    vec4 acceleration;      // x, y, z, padding
    vec4 prevAcceleration;  // x, y, z, padding
    vec4 orientation;       // Quaternion to (prevent gimbal lock)
    vec4 angularVelocity;   // Pseudo-vector for easy math
    vec4 angularAcceleration;       // Pseudo-vector for easy math
    vec4 prevAngularAcceleration;   // Pseudo-vector for easy math
    // Internal:
    vec4 signallingSubstances; // 4 substances for now
    int modeIndex;  // absolute index of the cell's mode
    float age; // also used for split timer
    float toxins;
    float nitrates;
    int adhesionIndices[20];
    
    // Lineage tracking (AA.BB.C format)
    uint parentLineageId;       // AA: Parent's unique ID (0 for root cells)
    uint uniqueId;              // BB: This cell's unique ID
    uint childNumber;           // C: Child number (1 or 2, 0 for root cells)
    uint _lineagePadding;       // Padding to maintain 16-byte alignment
};

struct GPUModeAdhesionSettings
{
    int canBreak;                 // bool -> int (4 bytes)
    float breakForce;
    float restLength;
    float linearSpringStiffness;
    float linearSpringDamping;
    float orientationSpringStiffness;
    float orientationSpringDamping;
    float maxAngularDeviation;
    float twistConstraintStiffness;
    float twistConstraintDamping;
    int enableTwistConstraint;    // bool -> int (4 bytes)
    int _padding;                 // pad to 48 bytes
};

// mode data structure for compute shader
struct GPUMode {
    vec4 color;           // R, G, B, padding
    vec4 orientationA;    // Quaternion
    vec4 orientationB;    // Quaternion
    vec4 splitDirection;  // x, y, z, padding
    ivec2 childModes;     // mode indices for children
    float splitInterval;
    int genomeOffset;
    GPUModeAdhesionSettings adhesionSettings;
    int parentMakeAdhesion; // Boolean flag for adhesion creation
    int childAKeepAdhesion; // Boolean flag for child A to keep adhesion
    int childBKeepAdhesion; // Boolean flag for child B to keep adhesion
    int maxAdhesions;       // Max number of adhesions
};

// Instance data structure for rendering
struct InstanceData {
    vec4 positionAndRadius;
    vec4 color;
    vec4 orientation;
};

// Input buffers
layout(std430, binding = 0) buffer CellBuffer {
    ComputeCell cellData[];
};

layout(std430, binding = 1) buffer ModeBuffer {
    GPUMode modes[];
};

layout(std430, binding = 2) buffer CellCountBuffer {
    uint cellCount;
    uint adhesionCount;
};

// Output buffers - separate for each LOD level
layout(std430, binding = 3) buffer InstanceBufferLOD0 {
    InstanceData instanceDataLOD0[];
};

layout(std430, binding = 4) buffer InstanceBufferLOD1 {
    InstanceData instanceDataLOD1[];
};

layout(std430, binding = 5) buffer InstanceBufferLOD2 {
    InstanceData instanceDataLOD2[];
};

layout(std430, binding = 6) buffer InstanceBufferLOD3 {
    InstanceData instanceDataLOD3[];
};

// LOD count buffer to track instances per LOD level
layout(std430, binding = 7) buffer LODCountBuffer {
    uint lodCounts[4];  // Count of instances for each LOD level
};

// Uniforms
uniform vec3 u_cameraPos;
uniform float u_lodDistances[4];  // Distance thresholds for LOD levels

void main() {
    uint index = gl_GlobalInvocationID.x;
    
    if (index >= cellCount) return;
    
    // Calculate cell radius and position
    vec3 cellPos = cellData[index].positionAndMass.xyz;
    float cellRadius = pow(cellData[index].positionAndMass.w, 1.0/3.0);
    
    // Calculate distance from camera to cell surface
    float distanceToCamera = distance(u_cameraPos, cellPos) - cellRadius;
    
    // Determine LOD level based on distance
    uint lodLevel = 3; // Default to lowest detail
    if (distanceToCamera < u_lodDistances[0]) {
        lodLevel = 0; // Highest detail
    } else if (distanceToCamera < u_lodDistances[1]) {
        lodLevel = 1; // High detail
    } else if (distanceToCamera < u_lodDistances[2]) {
        lodLevel = 2; // Medium detail
    }
    // else lodLevel = 3 (lowest detail)
    
    // Create instance data
    InstanceData instance;
    instance.positionAndRadius = vec4(cellPos, cellRadius);
    
    instance.color = modes[cellData[index].modeIndex].color;
    instance.orientation = cellData[index].orientation;
    
    // Atomically increment the count for this LOD level and get the write index
    uint writeIndex = atomicAdd(lodCounts[lodLevel], 1);
    
    // Write instance data to the appropriate LOD buffer
    if (lodLevel == 0) {
        instanceDataLOD0[writeIndex] = instance;
    } else if (lodLevel == 1) {
        instanceDataLOD1[writeIndex] = instance;
    } else if (lodLevel == 2) {
        instanceDataLOD2[writeIndex] = instance;
    } else { // lodLevel == 3
        instanceDataLOD3[writeIndex] = instance;
    }
} 
