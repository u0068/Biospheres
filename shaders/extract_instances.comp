#version 430 core

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

// Input: Full cell data from physics simulation

// mode data structure for compute shader
struct GPUMode {
    vec4 color;             // r, g, b, padding
    vec4 orientationA;      // quaternion
    vec4 orientationB;      // quaternion
    vec4 splitDirection;  // x, y, z, padding
    ivec2 childModes;
    float splitInterval;
    int genomeOffset;
};

// Cell data structure for compute shader
struct ComputeCell {
    // Physics:
    vec4 positionAndMass; // x, y, z, mass
    vec4 velocity;        // Fixed to match CPU layout
    vec4 acceleration;    // Fixed to match CPU layout
    vec4 orientation;     // angular stuff in quaternion to prevent gimbal lock
    vec4 angularVelocity;
    vec4 angularAcceleration;
    // Internal:
    vec4 signallingSubstances; // 4 substances for now
    int modeIndex;  // absolute index of the cell's mode
    float age; // also used for split timer
    float toxins;
    float nitrates;
    // Unique ID system
    uint uniqueID;     // Packed ID: [parent(16)] [cell(15)] [child(1)]
    uint padding1;     // Padding to maintain alignment
    uvec2 padding2;    // Additional padding to ensure 16-byte alignment
};

struct InstanceData {
    vec4 positionAndRadius;
    vec4 color;
    vec4 orientation;  // quaternion for rotation
};

layout(std430, binding = 0) buffer CellBuffer {
    // Cell structure: positionAndRadius, velocityAndMass, acceleration
    ComputeCell cellData[];
};

layout(std430, binding = 1) restrict buffer modeBuffer {
    GPUMode modes[];
};

// Output: Compact instance data for rendering
layout(std430, binding = 2) buffer InstanceBuffer {
    InstanceData instanceData[]; // Only position, radius and color
};

layout(std430, binding = 3) buffer CellCountBuffer {
    uint cellCount;
    uint pendingCellCount;
};

void main() {
    uint index = gl_GlobalInvocationID.x;
    
    if (index >= cellCount) return;
    
    float myRadius = pow(cellData[index].positionAndMass.w, 1./3.);
    instanceData[index].positionAndRadius = vec4(cellData[index].positionAndMass.xyz, myRadius);
    instanceData[index].color = modes[cellData[index].modeIndex].color;
    instanceData[index].orientation = cellData[index].orientation;
}
